/*
 * SPDX-FileCopyrightText: 2015 microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.1'
    }
}

def versionCode() {
    def stdout = new ByteArrayOutputStream()
    exec { commandLine 'git', 'rev-list', '--count', "HEAD"; standardOutput = stdout }
    return Integer.parseInt(stdout.toString().trim()) + 83700000
}

def versionName() {
    def stdout = new ByteArrayOutputStream()
    if (rootProject.file("gradlew").exists())
        exec { commandLine 'git', 'describe', '--tags', '--always', '--dirty'; standardOutput = stdout }
    else // automatic build system, don't tag dirty
        exec { commandLine 'git', 'describe', '--tags', '--always'; standardOutput = stdout }
    return stdout.toString().trim().substring(1)
}

allprojects {
    apply plugin: 'idea'
    version = "0.3.0.37526-lh"

    ext.androidBuildVersionTools = "30.0.3"

    ext.appVersionCode = 83762402
    ext.androidCompileSdk = 33
    ext.androidTargetSdk = 29
    ext.androidMinSdk = 21
    ext.iapVersion = "1.0.6"
}

subprojects {
    group = 'org.microg'
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
    }
}

